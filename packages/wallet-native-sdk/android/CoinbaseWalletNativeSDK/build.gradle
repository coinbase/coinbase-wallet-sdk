plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32

        versionCode 1
        versionName "0.1.0"

        buildConfigField "int", "LIBRARY_VERSION_CODE", "${versionCode}"
        buildConfigField "String", "LIBRARY_VERSION_NAME", "\"${versionName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation 'com.google.crypto.tink:tink-android:1.6.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.coinbase'
                artifactId 'coinbase-wallet-sdk'
                version = android.defaultConfig.versionName

                pom {
                    name = 'coinbase-wallet-sdk'
                    description = 'Coinbase Native Wallet SDK'
                    url = 'https://github.com/coinbase/coinbase-wallet-sdk'
                    licenses {
                        license {
                            name = 'Coinbase License'
                            url = 'https://github.com/coinbase/coinbase-wallet-sdk/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'amitgoelny'
                            name = 'Amit Goel'
                            email = 'amit.nyc@gmail.com'
                        }
                        developer {
                            id = 'bangtoven'
                            name = 'Jungho Bang'
                            email = 'me@bangtoven.com'
                        }
                        developer {
                            id = 'vishnumad'
                            name = 'Vishnu Mad'
                            email = 'vishnu.madhusoodanan@coinbase.com'
                        }
                    }
                    scm {
                        developerConnection = 'scm:git@github.com:coinbase/coinbase-wallet-sdk.git'
                        url = 'https://github.com/coinbase/coinbase-wallet-sdk/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
